Thermostat
| extend EnqueuedTimeUTC_dt = todatetime(EnqueuedTimeUTC)
| where EnqueuedTimeUTC_dt > ago(2d)
| where DeviceId == 'TH005'
| summarize avgTemp=avg(Temp) by bin(EnqueuedTimeUTC_dt, 1m)
| render timechart
 
//What will be the temperature for next 15 Minutes?
Thermostat
| extend EnqueuedTimeUTC_dt = todatetime(EnqueuedTimeUTC)
| where EnqueuedTimeUTC_dt > ago(2d)
| where DeviceId == 'TH005'
| make-series AvgTemp=avg(Temp) default=real(null) on EnqueuedTimeUTC_dt from ago(2d) to now()+15m step 1m
| extend NoGapsTemp=series_fill_linear(AvgTemp)
| project EnqueuedTimeUTC_dt, NoGapsTemp
| extend forecast=series_decompose_forecast(NoGapsTemp, 15)
| render timechart with(title='Forecasting the next 15min by Time Series Decomposition')
 
 
//Are there any anomalies for this device?
 
Thermostat
| extend EnqueuedTimeUTC_dt = todatetime(EnqueuedTimeUTC)
| where EnqueuedTimeUTC_dt > ago(12h)
| where DeviceId == 'TH005'
| make-series AvgTemp=avg(Temp) default=real(null) on EnqueuedTimeUTC_dt from ago(12h) to now() step 1m
| extend NoGapsTemp=series_fill_linear(AvgTemp)
| project EnqueuedTimeUTC_dt, NoGapsTemp
| extend anomalies=series_decompose_anomalies(NoGapsTemp, 1)
| render anomalychart with(anomalycolumns=anomalies)
 
  